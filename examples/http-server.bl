include "stdlib.bl"

const BUFCAP = 1024

struct sockaddr
    sa_family 2
    sa_data 14
end

struct in_addr
    s_addr 4
end

struct sockaddr_in
    sin_family 2
    sin_port 2
    sin_addr sizeof(in_addr)
    sin_zero 8
end

fun main do
    var sock_fd = socket(AF_INET, SOCK_STREAM, 0)
    if sock_fd < 0 then
        err_puts("ERROR: could not create socket\n")
        exit(1)
    end
    var servaddr = buffer(sizeof(sockaddr_in))
    memset(0, servaddr, 16)
    storef(sockaddr_in.sin_family, servaddr, AF_INET)
    storef(sockaddr_in.sin_addr, servaddr, 0)
    storef(sockaddr_in.sin_port, servaddr, 0x5000)
    if bind(sock_fd, servaddr, 16) < 0 then
        err_puts("ERROR: could not bind to socket\n")
        exit(1)
    end
    if listen(sock_fd, 5) != 0 then
        err_puts("ERROR: could not listen on socket\n")
        exit(1)
    end

    puts("INFO: Listening for connections\n")

    var client_addr = buffer(sizeof(sockaddr))
    var client_len = buffer(4)
    var readbuf = buffer(BUFCAP)
    while 1 do
        puts("Waiting to accept...\n")
        var cl_fd = accept(sock_fd, client_addr, client_len)
        puts("Accepted\n")
        if cl_fd < 0 then
            err_puts("ERROR: could not accept client\n")
            exit(1)
        end

        var bytes_read = read(cl_fd, readbuf, BUFCAP)
        puts(readbuf)

        var resp = "HTTP/1.1 200 OK\r\n"
        write(cl_fd, resp, strlen(resp))

        write(cl_fd, "\r\n", 2)

        var body = "This is a body\r\n"
        write(cl_fd, body, strlen(body))

        close(cl_fd)
    end

    close(sock_fd)

    return 0
end